package governance

struct ToolRegistration {
    toolName: Text,
    tag: Text
};

struct PendingRequest {
    requestId: Text,
    toolName: Text,
    callerIdentity: Text,
    callerClaims: Map<Text, Text>,
    arguments: Text,
    sessionId: Text,
    requestPayload: Text,
    status: Text,
    reason: Text,
    decidedBy: Text,
    executionStatus: Text,
    executionResult: Text
};

struct EvaluationResult {
    decision: Text,
    requestId: Text,
    message: Text
};

@api
protocol[pAdmin, pGateway] ServiceGovernance() {
    initial state active;

    var serviceName: Text = "";
    private var tools: Map<Text, ToolRegistration> = mapOf<Text, ToolRegistration>();
    private var pendingRequests: Map<Text, PendingRequest> = mapOf<Text, PendingRequest>();
    private var requestIdToKey: Map<Text, Text> = mapOf<Text, Text>();
    private var requestCounter: Number = 0;

    @api
    permission[pAdmin] setup(name: Text) | active {
        require(serviceName == "", "Already initialized");
        serviceName = name;
        info("[ServiceGovernance] Initialized for service: " + name);
    };

    @api
    permission[pAdmin] registerTool(toolName: Text, tag: Text) | active {
        require(tag == "open" || tag == "gated", "Tag must be 'open' or 'gated'");
        var reg = ToolRegistration(toolName = toolName, tag = tag);
        tools = tools.with(toolName, reg);
    };

    @api
    permission[pAdmin] setTag(toolName: Text, tag: Text) | active {
        require(tag == "open" || tag == "gated", "Tag must be 'open' or 'gated'");
        require(tools.getOrNone(toolName).isPresent(), "Tool not found");
        var reg = ToolRegistration(toolName = toolName, tag = tag);
        tools = tools.with(toolName, reg);
    };

    @api
    permission[pGateway] evaluate(
        toolName: Text,
        callerIdentity: Text,
        callerClaims: Map<Text, Text>,
        arguments: Text,
        sessionId: Text,
        requestPayload: Text
    ) returns EvaluationResult | active {
        var lookupKey = callerIdentity + ":" + toolName + ":" + arguments;
        var maybeExisting = pendingRequests.getOrNone(lookupKey);

        if (maybeExisting.isPresent()) {
            var existing = maybeExisting.getOrFail();

            if (existing.status == "approved") {
                pendingRequests = pendingRequests.without(lookupKey);
                requestIdToKey = requestIdToKey.without(existing.requestId);
                return EvaluationResult(decision = "allow", requestId = existing.requestId, message = "Approved");
            };

            if (existing.status == "denied") {
                var denyReason = existing.reason;
                pendingRequests = pendingRequests.without(lookupKey);
                requestIdToKey = requestIdToKey.without(existing.requestId);
                return EvaluationResult(decision = "deny", requestId = existing.requestId, message = denyReason);
            };

            return EvaluationResult(
                decision = "pending",
                requestId = existing.requestId,
                message = "Awaiting approval"
            );
        };

        requestCounter = requestCounter + 1;
        var requestId = "REQ-" + requestCounter.toText();

        var pending = PendingRequest(
            requestId = requestId,
            toolName = toolName,
            callerIdentity = callerIdentity,
            callerClaims = callerClaims,
            arguments = arguments,
            sessionId = sessionId,
            requestPayload = requestPayload,
            status = "pending",
            reason = "",
            decidedBy = "",
            executionStatus = "none",
            executionResult = ""
        );

        pendingRequests = pendingRequests.with(lookupKey, pending);
        requestIdToKey = requestIdToKey.with(requestId, lookupKey);

        return EvaluationResult(
            decision = "pending",
            requestId = requestId,
            message = "Awaiting approval"
        );
    };

    @api
    permission[pAdmin] approve(requestId: Text) | active {
        var maybeLookupKey = requestIdToKey.getOrNone(requestId);
        require(maybeLookupKey.isPresent(), "Request not found");
        var lookupKey = maybeLookupKey.getOrFail();

        var existing = pendingRequests.getOrNone(lookupKey).getOrFail();
        require(existing.status == "pending", "Request is not pending");

        var updated = PendingRequest(
            requestId = existing.requestId,
            toolName = existing.toolName,
            callerIdentity = existing.callerIdentity,
            callerClaims = existing.callerClaims,
            arguments = existing.arguments,
            sessionId = existing.sessionId,
            requestPayload = existing.requestPayload,
            status = "approved",
            reason = "",
            decidedBy = "admin",
            executionStatus = "queued",
            executionResult = existing.executionResult
        );

        pendingRequests = pendingRequests.with(lookupKey, updated);
    };

    @api
    permission[pAdmin] deny(requestId: Text, reason: Text) | active {
        var maybeLookupKey = requestIdToKey.getOrNone(requestId);
        require(maybeLookupKey.isPresent(), "Request not found");
        var lookupKey = maybeLookupKey.getOrFail();

        var existing = pendingRequests.getOrNone(lookupKey).getOrFail();
        require(existing.status == "pending", "Request is not pending");

        var updated = PendingRequest(
            requestId = existing.requestId,
            toolName = existing.toolName,
            callerIdentity = existing.callerIdentity,
            callerClaims = existing.callerClaims,
            arguments = existing.arguments,
            sessionId = existing.sessionId,
            requestPayload = existing.requestPayload,
            status = "denied",
            reason = reason,
            decidedBy = "admin",
            executionStatus = "none",
            executionResult = existing.executionResult
        );

        pendingRequests = pendingRequests.with(lookupKey, updated);
    };

    @api
    permission[pAdmin] getPendingRequests() returns List<PendingRequest> | active {
        var allValues = pendingRequests.values();
        return allValues.filter(function(req: PendingRequest) -> req.status == "pending");
    };
}
